use super::*;

grammar;

pub SequenceSetup : SequenceSetup<AlphaNumBytes> = {
    "cat:" <cat:SetupState> "dog:" <dog:SetupState> => SequenceSetup {
        cat,
        dog,
    },
};

SetupState: SetupState<AlphaNumBytes> = {
    <interests:Interests?> <state:State> => SetupState {
        interests: interests.unwrap_or_else(|| FixedInterests::full()),
        state,
    }
};

Interests: FixedInterests<AlphaNumBytes> = {
    "<" <first:Interest> <rest:("," <Interest>)*> ">" => {
        let mut interests = Vec::new();
        interests.push(first);
        interests.extend(rest.into_iter());
        FixedInterests(interests)
    }
};

Interest: RangeOpen<AlphaNumBytes> = {
    "(" <start:Word> "," <end:Word> ")" => (start, end).into()
};

State: BTreeMap<AlphaNumBytes, AlphaNumBytes> = {
    "[" <StateInner> "]" => <>
};

StateInner: BTreeMap<AlphaNumBytes, AlphaNumBytes> = {
    <first:KeyValue?> <rest:("," <KeyValue>)*> => {
        let mut state = BTreeMap::<AlphaNumBytes, AlphaNumBytes>::new();
        state.extend(rest.into_iter());
        if let Some(first) = first {
            state.insert(first.0, first.1);
        }
        state
    }
};

KeyValue: (AlphaNumBytes, AlphaNumBytes) = {
    <key:Word> ":" <value:Word> => (key.clone(), value),
}

Word : AlphaNumBytes = {
    r"[a-zA-Z0-9]*"=> AlphaNumBytes::from(<>.to_string()),
    r"𝚨"=> AlphaNumBytes::min_value(),
    r"𝛀"=> AlphaNumBytes::max_value(),
};

